#!perl

our $DATE = '2014-12-28'; # DATE
our $VERSION = '0.05'; # VERSION

use 5.010;
use strict;
use warnings;

use Perinci::CmdLine::Any -prefer_lite=>1;

our %SPEC;
$SPEC{list_dzil_bundle_contents} = {
    v => 1.1,
    summary => 'List plugins included in a Dist::Zilla plugin bundle',
    args => {
        bundle => {
            schema => 'str*',
            req    => 1,
            pos    => 0,
            completion => sub {
                require Complete::Dist::Zilla;
                my %args = @_;
                Complete::Dist::Zilla::complete_dzil_bundle(word=>$args{word});
            },
        },
        short => {
            schema => 'bool*',
            cmdline_aliases => {s=>{}},
        },
    },
};
sub list_dzil_bundle_contents {
    require Module::Load;

    my %args = @_;

    my $bundle = $args{bundle};
    $bundle =~ s/^Dist::Zilla::PluginBundle:://;
    $bundle =~ s!/!::!g; # convenience

    my $module = "Dist::Zilla::PluginBundle::$bundle";
    Module::Load::load($module);

    my $obj = $module->new(name => 'bundle', payload=>{});
    $obj->configure;

    my $plugins = $obj->plugins;
    my $res = [];
    for (@$plugins) {
        if ($args{short}) {
            $_->[1] =~ s/^Dist::Zilla::Plugin:://;
            push @$res, $_->[1];
        } else {
            push @$res, [$_->[0], $_->[1]];
        }
    }

    [200, "OK", $res];
}

Perinci::CmdLine::Any->new(
    url => '/main/list_dzil_bundle_contents',
)->run;

# ABSTRACT: List plugins included in a Dist::Zilla plugin bundle
# PODNAME: list-dzil-bundle-contents

__END__

=pod

=encoding UTF-8

=head1 NAME

list-dzil-bundle-contents - List plugins included in a Dist::Zilla plugin bundle

=head1 VERSION

This document describes version 0.05 of list-dzil-bundle-contents (from Perl distribution App-DzilUtils), released on 2014-12-28.

=head1 SYNOPSIS

 % list-dzil-bundle-contents Author::PERLANCAR

=head1 OPTIONS

C<*> marks required options.

=over

=item B<--bundle>=I<s>*

=item B<--config-path>=I<s>

Set path to configuration file.

Can be specified multiple times.

=item B<--config-profile>=I<s>

Set configuration profile to use.

=item B<--format>=I<s>

Choose output format, e.g. json, text.

=item B<--help>, B<-h>, B<-?>

Display this help message.

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=item B<--no-config>

Do not use any configuration file.

=item B<--short>, B<-s>

=item B<--version>, B<-v>

=back

=head1 FILES

B</etc/list-dzil-bundle-contents.conf>

B<~/list-dzil-bundle-contents.conf>

=head1 COMPLETION

This script has shell tab completion capability with support for several shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C list-dzil-bundle-contents list-dzil-bundle-contents

in your bash startup (e.g. C<~/.bashrc>). Your next shell session will then recognize tab completion for the command. Or, you can also directly execute the line above in your shell to activate immediately.

It is recommended, however, that you install L<shcompgen> which allows you to activate completion scripts for several kinds of scripts on multiple shells. Some CPAN distributions (those that are built with L<Dist::Zilla::Plugin::GenShellCompletion>) will even automatically enable shell completion for their included scripts (using C<shcompgen>) at installation time, so you can immadiately have tab completion.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete list-dzil-bundle-contents 'p/*/`list-dzil-bundle-contents`/'

in your tcsh startup (e.g. C<~/.tcshrc>). Your next shell session will then recognize tab completion for the command. Or, you can also directly execute the line above in your shell to activate immediately.

It is also recommended to install C<shcompgen> (see above).

=head2 other shells

For fish and zsh, install C<shcompgen> as described above.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-DzilUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/sharyanto/perl-App-DzilUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-DzilUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2014 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
