#!perl

our $DATE = '2014-08-16'; # DATE
our $VERSION = '0.01'; # VERSION

use 5.010;
use strict;
use warnings;

use App::DzilUtils;
use Perinci::CmdLine::Any -prefer_lite=>1;

our %SPEC;
$SPEC{list_dzil_plugin_roles} = {
    v => 1.1,
    summary => 'List roles included in a Dist::Zilla plugin',
    args => {
        plugin => {
            schema => 'str*',
            req    => 1,
            pos    => 0,
            completion => $App::DzilUtils::_complete_plugin,
        },
    },
};
sub list_dzil_plugin_roles {
    require Module::Load;

    my %args = @_;

    my $plugin = $args{plugin};
    $plugin =~ s/^Dist::Zilla::Plugin:://;
    $plugin =~ s!/!::!g; # convenience

    my $module = "Dist::Zilla::Plugin::$plugin";
    Module::Load::load($module);

    my $obj = $module->new(plugin_name => 'plugin', zilla=>bless({}, "Dist::Zilla"));

    my $roles = $obj->meta->roles;
    my $res = [];
    for my $role (@$roles) {
        my $name = $role->name;
        for (split /\|/, $name) {
            #next unless /^Dist::Zilla::Role::/;
            next if /__ANON__/;
            push @$res, $_;
        }
    }

    [200, "OK", $res];
}

Perinci::CmdLine::Any->new(
    url => '/main/list_dzil_plugin_roles',
)->run;

# ABSTRACT: List roles included in a Dist::Zilla plugin
# PODNAME: list-dzil-plugin-roles

__END__

=pod

=encoding UTF-8

=head1 NAME

list-dzil-plugin-roles - List roles included in a Dist::Zilla plugin

=head1 VERSION

This document describes version 0.01 of list-dzil-plugin-roles (from Perl distribution App-DzilUtils), released on 2014-08-16.

=head1 SYNOPSIS

 % list-dzil-plugin-roles MetaJSON

To active bash completion:

 % complete -C list-dzil-plugin-roles list-dzil-plugin-roles ; # can be put in bash startup file e.g. .bashrc
 % list-dzil-plugin-roles meta<tab>
 % list-dzil-plugin-roles test/<tab>   ; # use / to separate namespace
 % list-dzil-plugin-roles "Test::<tab> ; # or :: but use quotes

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-DzilUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/sharyanto/perl-App-DzilUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-DzilUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

Steven Haryanto <stevenharyanto@gmail.com>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2014 by Steven Haryanto.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
